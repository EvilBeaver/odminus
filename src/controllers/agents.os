#Использовать irac

Функция Index() Экспорт
	
	ТаблицаАгентов = ЦентральныеСерверы.ПолучитьСписок();
	Возврат Представление(ТаблицаАгентов);

КонецФункции

Функция Add() Экспорт

	Если ЗапросHttp.Метод = "POST" Тогда
		
		НовыйID  = ЗапросHttp.ДанныеФормы["Идентификатор"];
		ТЗ = ЦентральныеСерверы.ПолучитьСписок();
		Элементы = ТЗ.НайтиСтроки(Новый Структура("Идентификатор", НовыйID));
		Элемент = ТЗ.Добавить();
		Если Элементы.Количество() > 0 Тогда
			СостояниеМодели.ДобавитьОшибку("Идентификатор","Такой ID уже есть в списке агентов");
			СохранитьДанные(Элемент);
			Возврат Представление("Item", Элемент);
		КонецЕсли;

		СохранитьДанные(Элемент);
		ЦентральныеСерверы.Записать(ТЗ);

		Возврат Перенаправление("/agents/index");
	КонецЕсли;
	
	Возврат Представление("Item");

КонецФункции

Функция Edit() Экспорт

	Идентификатор = ЗначенияМаршрута["id"];
	Если Идентификатор = Неопределено Тогда
		Возврат Перенаправление("/agents/index");
	КонецЕсли;

	ТЗ      = ЦентральныеСерверы.ПолучитьСписок();
	Элемент = ТЗ.Найти(Идентификатор,"Идентификатор");
	
	Если Элемент = Неопределено Тогда
		Возврат КодСостояния(404);
	КонецЕсли;

	Если ЗапросHttp.Метод = "POST" Тогда
		
		СтарыйID = Идентификатор;
		НовыйID  = ЗапросHttp.ДанныеФормы["Идентификатор"];
		Элементы = ТЗ.НайтиСтроки(Новый Структура("Идентификатор", НовыйID));

		Если Элементы.Количество() > 1 Тогда
			СостояниеМодели.ДобавитьОшибку("Идентификатор","Такой ID уже есть в списке агентов");
			СохранитьДанные(Элемент);
			Элемент.Идентификатор = СтарыйID;
			Возврат Представление("Item", Элемент);
		КонецЕсли;

		СохранитьДанные(Элемент);
		ЦентральныеСерверы.Записать(ТЗ);
		
		Возврат Перенаправление("/agents/index");
	Иначе
		// Передаем в представление "модель" - Элемент
		Возврат Представление("Item", Элемент);
	КонецЕсли;
	
КонецФункции

Процедура СохранитьДанные(Знач Элемент)
	
	ДанныеФормы = ЗапросHttp.ДанныеФормы;
	Элемент.Идентификатор = ДанныеФормы["Идентификатор"];
	Элемент.СетевоеИмя = ДанныеФормы["СетевоеИмя"];
	Элемент.Порт = ДанныеФормы["Порт"];
	Элемент.Описание = ДанныеФормы["Описание"];
	Элемент.Режим = "RAS";

КонецПроцедуры

Функция Delete() Экспорт

	Идентификатор = ЗначенияМаршрута["id"];
	Если Идентификатор = Неопределено Тогда
		Возврат Перенаправление("/agents/index");
	КонецЕсли;

	ТЗ      = ЦентральныеСерверы.ПолучитьСписок();
	Элемент = ТЗ.Найти(Идентификатор, "Идентификатор");

	Если Элемент = Неопределено Тогда
		Возврат КодСостояния(404);
	КонецЕсли;
		
	Если ЗапросHttp.Метод = "POST" Тогда
		
		ТЗ.Удалить(Элемент);
		ЦентральныеСерверы.Записать(ТЗ);
		
		Возврат Перенаправление("/agents/index");

	КонецЕсли;
	
	Возврат КодСостояния(405);

КонецФункции

Функция Clusters() Экспорт
	
	Идентификатор = ЗначенияМаршрута["id"];
	Если Идентификатор = Неопределено Тогда
		Возврат Перенаправление("/agents/index");
	КонецЕсли;

	ТЗ      = ЦентральныеСерверы.ПолучитьСписок();
	Элемент = ТЗ.Найти(Идентификатор, "Идентификатор");
	Если Элемент = Неопределено Тогда
		Возврат КодСостояния(404);
	КонецЕсли;

	Администрирование = Новый АдминистрированиеКластера(
		Элемент.СетевоеИмя,
		Элемент.Порт,
	);

	Кластеры = Администрирование.Кластеры();
	Возврат Представление(Кластеры);

КонецФункции